plugins {
	//AGP
    id "com.android.application"
	//KGP
    id "kotlin-android" 
	
    // The Flutter Gradle Plugin must be applied after the Android and Kotlin Gradle plugins.
    id "dev.flutter.flutter-gradle-plugin"
}


ext.abiCodes = ["armeabi-v7a": 1, "arm64-v8a": 2]

android {
	//Your project is configured with Android NDK 23.1.7779620, but the following plugin(s) depend on a different Android NDK version:
	//- path_provider_android requires Android NDK 27.0.12077973
	//- sqflite requires Android NDK 27.0.12077973
	//- url_launcher_android requires Android NDK 27.0.12077973
    namespace = "io.flutter.banguLite"
    compileSdk = flutter.compileSdkVersion
    //ndkVersion = flutter.ndkVersion
	ndkVersion = "27.0.12077973"

	compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
		encoding="UTF-8"
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17
    }

    defaultConfig {
        // TODO: Specify your own unique Application ID (https://developer.android.com/studio/build/application-id.html).
        applicationId "io.flutter.banguLite"
        // You can update the following values to match your application needs.
        // For more information, see: https://flutter.dev/to/review-gradle-config.
        minSdk = flutter.minSdkVersion
        targetSdk = flutter.targetSdkVersion
        versionCode = flutter.versionCode
        versionName = flutter.versionName
		
    }

	applicationVariants.all { variant ->
		variant.outputs.all { output ->
			def abi = output.getFilter(com.android.build.OutputFile.ABI)
			if (abi != null) {
				outputFileName = "banguLite-${variant.versionName}-${abi}.apk"
				def abiVersionCode = project.ext.abiCodes.get(abi)
				if (abiVersionCode != null) {
					versionCodeOverride = variant.versionCode * 10 + abiVersionCode
				}
			} 
			
			else {
				outputFileName = "banguLite-${variant.versionName}.apk"
				versionCodeOverride = variant.versionCode * 10
			}

		}
	}




    buildTypes {
        release {
            // TODO: Add your own signing config for the release build.
            // Signing with the debug keys for now, so `flutter run --release` works.
            signingConfig = signingConfigs.debug
			minifyEnabled true
        }

		
        

		//debug {
        //    signingConfig signingConfigs.debug
		//	//buildDir = "build/outputs/apk" // 指定输出目录
            
        //    //setProperty("archivesBaseName", "app-${variant.name}")
        //}
    }


    //flavorDimensions = ["development","production"]
	//flavorDimensions "environment"
	flavorDimensions = ["environment"]

    productFlavors{
        development{
            dimension "environment"
			applicationId "io.flutter.banguLite.dev"  // 明确指定包名
        }
        production{
            dimension "environment"
			applicationId "io.flutter.banguLite"  // 明确指定包名
        }
    }


    splits{
        // 基于不同的abi架构配置不同的apk
        abi {

          // 必须为true，打包才会为不同的abi生成不同的apk
          enable true
         
          // 默认情况下，包含了所有的ABI。
          // 所以使用reset()清空所有的ABI，再使用include指定我们想要生成的架构armeabi-v7a、arm-v8a
          reset()

          // 逗号分隔列表的形式指定 Gradle 应针对哪些 ABI 生成 APK。只与 reset() 结合使用，以指定确切的 ABI 列表。
          // include "armeabi-v7a", "arm64-v8a"

          //noinspection ChromeOsAbiSupport
          include "arm64-v8a","armeabi-v7a"

          // 是否生成通用的apk，也就是包含所有ABI的apk。如果设为 true，那么除了按 ABI 生成的 APK 之外，Gradle 还会生成一个通用 APK。
          universalApk false
        }
    }
}

flutter {
    source = "../.."
}
